!---|----1----|----2----|----3----|----4----|----5----|----6----|----7----|---;
! Tracking test SYNCHR. OSC. with LHC  seq.060203  
! tstTrack_synchr_LHC.madx

option, -echo,-warn,-info;

! ********** Define sequence
call, file="/afs/cern.ch/eng/lhc/optics/V6.503/V6.5.thin.seq";
call, file="/afs/cern.ch/eng/lhc/optics/V6.503/V6.5.thin.inj.str";

! ********** Define beam
beam, sequence= lhcb1, particle= proton, energy= 450;


! ********** If present, remove some elements using seqedit
option, echo,warn,info;
seqedit, sequence= lhcb1;
  remove, element= ACN.A5L4.B1;
  remove, element= ACN.B5L4.B1;
  remove, element= ACN.C5L4.B1;
  remove, element= ACN.D5L4.B1;
  remove, element= ADTH.A5L4.B1;
  remove, element= ADTH.B5L4.B1;
  remove, element= APW.5R4.B1;
  remove, element= ADTV.A5R4.B1;
  remove, element= ADTV.B5R4.B1;
endedit;

! ********** Twiss
use, sequence= lhcb1;
select, flag= twiss, clear;
select, flag= twiss, pattern= "IP\.*", column= name,betx,bety,alfx,alfy,dx;
select, flag= twiss, class= rfcavity,  column= name,betx,bety,alfx,alfy,dx;
TWISS,  file= "out_tstTrack_synchr_LHC.twissprb1.1";
system, "cat out_tstTrack_synchr_LHC.twissprb1.1"; 

! ********** Make a thin sequence
option, -echo,-warn,-info;
select, flag= makethin, pattern= "MQXA.*", slice= 12;
select, flag= makethin, pattern= "MQXB.*", slice= 12;
select, flag= makethin, class= quadrupole, slice= 2;
makethin, sequence= lhcb1;


! ********** Track three particles
option, echo,warn,info;
use, sequence= lhcb1;
vrf400= 8.0;   !   Qs=0.0057
track, dump;
  start, pt= 0.0001;
  start, pt= 0.001002; ! edge of bucket
  start, pt= 0.001004; ! just outside of bucket
!  run,   turns= 160; !,tolerance={.1,.1,.1,.1,100.,.1};!175=1 synchr. period
  run,   turns= 320; !,tolerance={.1,.1,.1,.1,100.,.1};!175=1 synchr. period
endtrack; 


! ********** Plot the end distribution
system, "gnuplot < tstTrack_synchr_LHC.gnuplot";


return;

!save, file= "V6.4.thin.b1.seq", sequence= lhcb1;  ! save thin sequence
!---|----1----|----2----|----3----|----4----|----5----|----6----|----7----|---;

! ********** Plot twiss data
use, sequence= lhcb1, range= ip8/BPMSW.1L1.B1; ! to print close to IP1 upstream
select, flag= twiss, clear;
select, flag= twiss, column= name,s;
TWISS,  file= "out_tstTrack_synchr_LHC.twissprb1", betx= 10, bety= 10;
system, "cat out_tstTrack_synchr_LHC.twissprb1"; 

select, flag= twiss, clear;
select, flag= twiss, pattern= "IP\.*", column= name,betx,bety,alfx,alfy,dx,s;
select, flag= twiss, class= rfcavity,  column= name,betx,bety,alfx,alfy,dx;
TWISS,  file= "out_tstTrack_synchr_LHC.twissprb1";
system, "cat out_tstTrack_synchr_LHC.twissprb1"; 

plot, haxis= t, vaxis= pt, symbol= 2, table= track;
plot, haxis= s, vaxis= betx,bety;




