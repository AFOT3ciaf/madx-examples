! Simple tracking test of synchrotron oscillations with FODO.
! tstTrack_syn.madx

option,-echo,-info,-warn;

! **********  Elements 
corh:  hkicker,    kick= 0.00; !0001;
qf:    multipole,  knl= {0,0.05001};
qd:    multipole,  knl= {0,-0.05};
dip:   multipole,  knl= {twopi/30.0}; ! ,lrad=1111.; 
halfd: drift,      l= 5.0;
endmk: marker;
trfc:  rfcavity ,  volt= 0.4, harmon= 3000, lag= 0.5;

! ********** Define sequences
smach: sequence, l= 40.0;
corh,  at= 0;
qf,    at= 0;
dip,   at= 5.0;
qd,    at= 10.0;
dip,   at= 15.0;
trfc,  at= 20.0;
!endmk, at= 20.0;
qf,    at= 20.0;
dip,   at= 25.0;
qd,    at= 30.0;
dip,   at= 35.0;
endmk, at= 40.0;
endsequence;


! ********** Define beam and use the sequence with it 
beam,  sequence= smach, particle= proton, energy= 200;
use,   sequence= smach;


! ********** TWISS
twiss;


! ********** Track 3 particles with different PT
track, dump, aperture;
  start,   pt= 0.00002372;
  start,   pt= 0.000015;
  start,   pt= 0.00001;
  observe, place= qd[2];
  run,     turns= 120;
endtrack;

! ********** Plot the end distribution
system, "gnuplot < tstTrack_syn.gnuplot";

!system, "rm track*";

return;

