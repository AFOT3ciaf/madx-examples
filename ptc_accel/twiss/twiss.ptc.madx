!Example script showing calculation of the TWISS parameters with accelaration using PTC
!It also illustrates usage of PTC_SELECT command that inststructs PTC_TWISS to
!store given monomials cofficients in a given table 
!author: Piotr Skowronski (CERN)

!option,-echo;
!option,-info;

LQL := 0.220; ! length of QL
LQS := 0.124; ! length of QS
CAVLENGTH := 2.4;
QL  : QUADRUPOLE, L=LQL; ! QL quadrupole
QS  : QUADRUPOLE, L=LQS; ! QS quadrupole

!****** define the regular linac cell 
!setting up Travelling Wave Cavity, Tension 14MV on 2.4 m length, freq 3GHz, 30deg off crest
TCAV      : TWCAVITY,  L=CAVLENGTH, VOLT=14.0d0, LAG=30.d0/360., FREQ=3000.; 
TCAVDrift      : DRIFT, L=CAVLENGTH; 
meterdrif      : DRIFT, L=CAVLENGTH; 

ds_ql     : drift, l=0.25;    ! short drift between quads in Daniels design
ds_trip   : drift, l=(3.34-CAVLENGTH)/2.; ! long drift between quads in Daniels design
cell_mrk  : marker;
cell_q1   : marker;
cell_q3   : marker;
beforecav : marker;
bc : marker;
aftercav  : marker;
k_ls1     := -1.53125*.56/LQL; ! weaker foc.
k_ls2     :=  1.53125/LQL;     ! weaker foc.
quad_l1   : quadrupole, l=LQL,   k1=k_ls1;
quad_l2   : quadrupole, l=LQL/2, k1=k_ls2;
LDLS      :=(LQL-LQS)/2.; ! half L difference of QL and QS
dls       : DRIFT, L=LDLS; ! 0.048 m

!                  0.11  + 0.25 + 0.22 +     (3.34-2.4)/2.
ctfcell  : line=( quad_l2,ds_ql,quad_l1,cell_q1,ds_trip ,cell_mrk,
                  beforecav, 
                  TCAV,
                  aftercav,
                  ds_trip,cell_q3,quad_l1,ds_ql,quad_l2);
ctf   : line=(3*ctfcell);

! calculate Twiss parameters for regular cell structure
beam, PARTICLE=ELECTRON, ENERGY=0.02d0;


!First we calculate twiss parameters with thin lense approximation, 
!substituting travelling wave cavities with drift, 
!with close orbit

USE, period=ctf;

savebeta, place=#e, label=betaend;
twiss, file=betasNC.txt, rmatrix;

value, betaend->betx;

PLOT, file="ctfcell", haxis=s, vaxis=betx,bety, colour=100, 
      title="Standard TWISS";

select,flag=ptc_twiss,column=name,energy,s,beta11,beta21,beta12,beta22,disp1,disp3,x,px,y,py;


!make ctf with travelling wave cavities setup the active one
USE, period=ctf;

ptc_create_universe;
ptc_create_layout, model=1, method=6, nst=20, exact=true, closed_layout=false;
ptc_setswitch, debuglevel=0, maxacceleration=true, exact_mis=true, time=true, totalpath=true, fringe=true;

!calculate twiss parameters along the line, using initial parameters from the thin lense
ptc_twiss, icase=6, no=3, betx=betaend->betx, alfx=betaend->alfx,  bety=betaend->bety, alfy=betaend->alfy, betz=1.0;

PLOT, file="ctfcell", ptc=true, haxis=s, vaxis=beta11,beta22, colour=100, title="PTC TWISS";
write, table=PTC1, file="PTC1.txt";
write, table=ptc_twiss, file="ptc_twiss.txt";

PLOT, file="ctfcell", ptc=true, haxis=s, vaxis=energy, colour=100, title="";

ptc_end;
stop;
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!  SSSSS  TTTTT   OOO   PPP
!  S        T    O   O  P  P
!  SSSSS    T    O   O  PPP
!      S    T    O   O  P
!  SSSSS    T     OOO   P
!

