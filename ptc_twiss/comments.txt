======== V.Kapin, March, 2006 =======================================
This file is line-by-line comments of Mad-X input files in 
directories "Example1,...4" of PTC_TWISS examples
=====================================================================

At the beginning of all examples, the ring parameters are calculated:  
The ring lattice parameters are called: 

     CALL, file = "../ring_lattice/ring.seq";

Then, the TWISS parameters at the end of ring are calculated by two 
methods, using the TWISS command (Courant & Snyder parameters) and 
the PTC_TWISS command (Ripken-style parameters). 

For the calculations with TWISS command, the end (PLACE=#E) of the 
ring (SEQUENCE=fivecell) is marked by the label "TWISSip", demanding from 
a next TWISS command to fill a BETA0-block named as "TWISSip" with 
the values of all lattice parameters at the end of the ring.

    SAVEBETA, label=TWSSip, place=#E,sequence=fivecell;

The content of the twiss TFS file (here,"twiss_madx_ring") provided by 
the following TWISS command is defined by the command: 

    SELECT,   flag=twiss,column=name,s,betx,bety,dx,dy;

The Twiss parameters around the ring are calculated and written into 
the file "twiss_madx_ring" as the twiss TFS table:  

    TWISS,file=twiss_madx_ring;

The Twiss parameters at the end point of the ring are written to 
the MAD-X output (here, the file "example1.out") with the following 
command:

    SHOW,TWSSip;

The corresponding fragment of the Mad-X looks as the following lines: 
         command: beta0  module: control
         parameter: betx   double value:        177.7010914
         .....................................................
         parameter: r22   double value:                  0
         parameter: energy   double value:                100


For running the PTC_TWISS command, it is necessary to initialize PTC and 
apply misalignments to the current PTC layout. The alignment errors are 
defined in the MadX input files for the ring.

    PTC_CREATE_UNIVERSE;
    PTC_CREATE_LAYOUT,model=2,method=6,nst=10,exact;
    PTC_ALIGN;
  
The content of the twiss TFS file (here,"twiss_ptc_ring") provided by 
the following PTC_TWISS command is defined by the command: 

    SELECT,flag=ptc_twiss,column=name,s,beta11,beta21,
                             beta12,beta22,disp1,disp3,x,px,y,py;

The Ripken-style parameters are written to the file "twiss_ptc_ring"
as the twiss TFS table using the following command:

    PTC_TWISS,closed_orbit,icase=5,file=twiss_ptc_ring,no=1;

The following PTC_NORMAL command without the option "normal" works  
with the map (not with normal-form) of ring and generates a map-table.
The resulting first order map (NO=1) is linear one (the matrix).

    PTC_NORMAL,closed_orbit,maptable,icase=5,no=1;
    
The calculations with a current PTC layout for the ring are finished by:

    END_PTC;

The map table generated with the above PTC_TWISS can be written to 
the file "ring_matrix_at_end" with the following command:

    WRITE,table="map_table",file="ring_matrix_at_end";

At the second step of examples, the parameters of the transferline 
are calculated. The transferline lattice parameters are called: 

    CALL, file = "../line_lattice/line.seq";

With clearing of previous selections, the content of the twiss 
TFS file (here,"twiss_ptc_line") provided by below PTC_TWISS 
command is defined by the commands:

    SELECT,flag=ptc_twiss,clear;
    SELECT,flag=ptc_twiss,column=name,s,
                       beta11,beta21,beta12,beta22,disp1,disp3;

In order to apply the PTC_TWISS command to the transferline, 
it is necessary to initialize PTC again with following commands:

    PTC_CREATE_UNIVERSE;
    PTC_CREATE_LAYOUT,model=2,method=6,nst=5,exact;

Then, PTC_TWISS command can be applied to calculate the Ripken-style
parameters along the transferline using different parameters 
of the ring at its end as the initial conditions.The resulting 
parameters are written to the file "twiss_ptc_line" as the 
twiss TFS table. Four examples presented in the directories 
"Example1",.., "Example4" demonstrate four approaches for the 
input of the initial conditions. One of the four below commands 
should be run for a particular example:

1) as Twiss parameters from the BETA0 block (named as "TWSSip");
     
   PTC_TWISS,icase=5,no=1,BETA0=TWSSip,file=twiss_ptc_line;

2) as Twiss parameters given as arguments of the PTC_TWISS command;

   PTC_TWISS,icase=5,no=1, betx=177,bety=32,alfx=-2.4,alfy=0.4,
             dx=2.1,dpx=0.02,mux=1.2, muy=1.25, x=8.8E-05,
             px=7.2E-07,y=5.25E-05,py=-6.7E-07, 
                                       file=twiss_ptc_line;

3) as a transfer matrix from the map table generated by the above 
   PTC_NORMAL command;

   PTC_TWISS,icase=5,no=1,initial_matrix_table, file=twiss_ptc_line;

4) as transfer matrix  given as arguments of the PTC_TWISS command;
   PTC_TWISS,icase=5,no=1,initial_matrix_manual,              
    re11=-2.4,   re12=176., re13=0.23E-01, re14=1.06,      re16=2.3,
    re21=-.3E-01,re22=2.3,  re23=0.59E-03, re24=0.2E-01,   re26=0.4E-01,
    re31=-.1E-01,re32=1.1,  re33=0.44,     re34=32.7,      re36=0.23E-02,
    re41=-.1E-03,re42=0.01, re43=-.3E-01,  re44=-.535,     re46=-.9E-03,
                                                     file=twiss_ptc_line;

Example1 and Example3 demonstrate an internal ("automatic") method 
ptc_twiss,icase=5,no=1,initial_matrix_table, file=twiss_ptc_line;
for the input of the initial conditions, whereas Example2 and 
Example4 use an external ("manual") input of parameters in the 
command-line.   

To close the PTC universe and to end MAD-X execution, the final  
commands are   

   PTC_END;
   STOP;
