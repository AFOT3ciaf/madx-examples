ptc_enforce6d, flag=true;

dsel :=1.2;!length of starting and ending drift
dmid1l:=1.5;!length of middle drift
dmid2l:=0.6;!length of middle drift
! This example illustrates two features of MADXP
! - matching of higher order map elements
! - placemant of elements at arbitrary position


ql   :=0.2;!length of quads
qfstr := 6.695198761;!strength of quads
qdstr :=-6.712879942;!strength of quads
lbend:=2.0;

sedft: drift, L:=dsel;
middft1: drift, L:=dmid1l;
middft2: drift, L:=dmid2l;

angb = pi/6.;


!bend1: MULTIPOLE, L=lbend,
bend1: RBEND, L=lbend, ANGLE:=-angb, e1=angb/2, ptcrbend=true, APERTYPE=CIRCLE, APERTURE={10};             ! Deflection to the left
qf1: quadrupole, l=ql, k1:=qfstr, APERTYPE=CIRCLE, APERTURE={10.0};
qd:  quadrupole, l=ql, k1:=qdstr, APERTYPE=CIRCLE, APERTURE={10.0};
qf2: quadrupole, l=ql, k1:=qfstr, APERTYPE=CIRCLE, APERTURE={10.0};
bend2: RBEND, L=lbend, ANGLE=angb, e1=-angb/2, ptcrbend=true, APERTYPE=CIRCLE, APERTURE={10};              ! Deflection to the right
theend: marker, APERTYPE=CIRCLE, APERTURE={10.0};

chicane: line=(sedft,bend1,middft1,qf1,middft2,qd,middft2,qf2,middft1,bend2,sedft,theend);

beam, PARTICLE=ELECTRON, ENERGY=0.11d0;
use, period=chicane;




!creates a table
create, table=R1, column=_name,"c11","c12","c21","c22";
!instructs ptc to store given monomials cofficients in the just created table
ptc_select, table=R1, column="c11", polynomial=1, monomial="100000";
ptc_select, table=R1, column="c12", polynomial=1, monomial="010000";
ptc_select, table=R1, column="c21", polynomial=2, monomial="100000";
ptc_select, table=R1, column="c22", polynomial=2, monomial="010000";


create, table=R3, column=_name,"c51","c52","c55","c56","c66";
ptc_select, table=R3, column="c51", polynomial=5, monomial="100000";
ptc_select, table=R3, column="c52", polynomial=5, monomial="010000";
ptc_select, table=R3, column="c55", polynomial=5, monomial="000010";
ptc_select, table=R3, column="c56", polynomial=5, monomial="000001";
ptc_select, table=R3, column="c66", polynomial=6, monomial="000001";


create, table=T2,  column=_name,"c111","c112","c113","c566","c556","c656","c666";
!instructs ptc to store given monomials cofficients in the just created table
ptc_select, table=T2, column="c111", polynomial=1, monomial="200000";
ptc_select, table=T2, column="c112", polynomial=1, monomial="110000";
ptc_select, table=T2, column="c113", polynomial=1, monomial="101000";
ptc_select, table=T2, column="c566", polynomial=5, monomial="000002";
ptc_select, table=T2, column="c556", polynomial=5, monomial="000011";
ptc_select, table=T2, column="c666", polynomial=6, monomial="000002";
ptc_select, table=T2, column="c656", polynomial=6, monomial="000011";

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!


myvar := table(T2,c566);
select,flag=twiss,column=name,s,myvar,beta11,beta12,alfa11,alfa12;

dupax:=0;

ptc_create_universe;
ptc_create_layout, model=1, method=6, nst=10, exact=true, closed_layout=false;
ptc_setswitch, debuglevel=0, exact_mis=true, time=true, totalpath=true;



match, use_macro;
  vary, name=dupax, step=0.01,lower=-0.09,upper=0.09;
  m1: macro=
   {
      value, dupax;

      ptc_eplacement, range=qf1[1], x= dupax, y=0, z=0, onlyposition, refframe=previouselement, autoplacedownstream=false; 
      ptc_eplacement, range=qf2[1], x=-dupax, y=0, z=0, onlyposition, refframe=previouselement, autoplacedownstream=false; 

      ptc_twiss, table=twiss, icase=56, no=3, betx=10., alfx=0.0,  bety=10., alfy=0.0, betz=10., alfz=0.0;

      value, table(twiss,qf1,beta11),  table(twiss,qf1,beta12);
      value, table(twiss,theend,beta11), table(twiss,theend,beta12), table(twiss,theend,beta13);

      value, table(R3,theend,c51), table(R3,theend,c52), table(R3,theend,c56);

      value, table(T2,theend,c111), table(T2,theend,c112), table(T2,theend,c113),
             table(T2,theend,c566), table(T2,theend,c556),
             table(T2,theend,c666), table(T2,theend,c656);
   
      value, table(twiss,theend,disp1);
      value, table(twiss,middft1[1],x), table(twiss,middft2[1],x), table(twiss,middft2[2],x);

      
      value, dupax ;
      
      print, text="";
      print, text="Macro ened";
      print, text="#################################################";
      print, text="";
   };


!   constraint, expr=table(R3,theend,c51) = 0;
!   constraint, expr=table(R3,theend,c52) = 0;
!   constraint, expr=table(R3,theend,c56) = 0.4;
   constraint, expr=table(T2,theend,c566) = 6;

   
   constraint, weight=1000, expr=table(twiss,middft1[1],x)< 0.5;
   constraint, weight=1000, expr=table(twiss,middft1[1],x)>-0.5;

   constraint, weight=1000, expr=table(twiss,middft2[1],x)< 0.5;
   constraint, weight=1000, expr=table(twiss,middft2[1],x)>-0.5;

   constraint, weight=1000, expr=table(twiss,middft2[2],x)< 0.5;
   constraint, weight=1000, expr=table(twiss,middft2[2],x)>-0.5;

   lmdif,  tolerance=1e-9 ,calls=200;
endmatch;

ptc_end;



write, table=R1, file=R1.txt;
write, table=T2, file=T2.txt;
write, table=R3, file=R3.txt;
write, table=twiss, file=twiss.txt;



PLOT, noversion=true, haxis=s, vaxis=beta11,beta12, colour=100, 
    title="Chicane with decentered quads";

PLOT, noversion=true, haxis=s, vaxis=beta13, colour=100, 
    title="Chicane with decentered quads";

PLOT, noversion=true, haxis=s, vaxis=myvar, colour=100, 
    title="Chicane with decentered quads C566";

PLOT, noversion=true, haxis=s, vaxis=disp1, colour=100, 
    title="Chicane with decentered quads";


stop;

