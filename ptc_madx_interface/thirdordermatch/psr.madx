TITLE "PSR Ring"

option,echo,rbarc=false;
QDK1 := 0.558619275157041;
        Dm: DRIFT, L:= -1.1;
        Dp: DRIFT, L:= 1.1;
        D1: DRIFT, L:= 2.28646;
        D2: DRIFT, L:= 0.45;
        QD  :  QUADRUPOLE, L:=0.5, K1:= QDK1, tilt=0.0*pi/180;
        QF  :  QUADRUPOLE, L:=0.5, K1:=-0.394319488346147, tilt=0.0*pi/180;
        trueB   :  RBEND,  L:=2.54948, ANGLE:= 0.628318530717959, E1:=0.0, E2:=0.0,ptcrbend=true,truerbend=true;
        wedgB   :  RBEND,  L:=2.54948, ANGLE:= 0.628318530717959, E1:=0., E2:=0.0,ptcrbend=true,truerbend=false;
        B   :   RBEND,  L:=2.54948, ANGLE:= 0.628318530717959, E1:=0., E2:=0.;
        Bs   :  QUADRUPOLE,  L:=2.507749201408435, k1:=0.0;

        B1  :  RBEND,  L:=2.5, ANGLE:= -0.628318530717959, E1:=0.628318530717959/2.0, E2:=0.0,ptcrbend=true,truerbend=true;
        B2  :  RBEND,  L:=2.5, ANGLE:=  0.628318530717959, E1:=0.628318530717959/2.0, E2:=0.0,ptcrbend=true,truerbend=true;
        Q1  :  QUADRUPOLE, L:=0.5, K1:= 0.0;
        Q2  :  QUADRUPOLE, L:=0.5, K1:= 0.0;
        Q3  :  QUADRUPOLE, L:=0.5, K1:= 0.0;
        D : DRIFT, L:= 2.0;
        BHdown :   RBEND,  L:=0.2, ANGLE:= 0.1, E1:=0., E2:=0., tilt=-pi/2;
        BHup :     RBEND,  L:=0.2, ANGLE:= 0.1, E1:=0., E2:=0., tilt= pi/2;
        RF: RFCAVITY, L:=0.45, VOLT:=50, FREQ:=1;
    

!CELL:    LINE=( D1,QD,D2,B,dm, dp,D2,QF,D1);
CELL:    LINE=( D1,QD,RF,B,dm, dp,D2,QF,D1);
CHIC:    LINE=( BHdown, D2,BHup, D2,RF,D2, BHup, D2, BHdown);
CELLH:     LINE=( CELL, CHIC, CELL );

PSR:     LINE=( 10*CELL );
onebs:     LINE=( Bs );


eg :=  1.73527443433878; !  ... in GeV
beam, particle = proton, energy = eg; 
use, period=PSR;

select, flag=twiss, clear;
select, flag=twiss, columns=name, s, beta11, beta22, beta33, mux, muy;

create, table=H3, column=_name,"h0000";
!instructs ptc to store given monomials cofficients in the just created table
ptc_select, table=H3, column="h0000", polynomial=1, monomial="300000";

select_ptc_normal, dq1=1, dq2=1;
select_ptc_normal, dq1=2, dq2=2;


dQxdX:= table(normal_results,value,1);
dQydX:= table(normal_results,value,2);
dQxdX2:= table(normal_results,value,3);
dQydX2:= table(normal_results,value,4);!4th row in table normal_results

match, use_macro;
  vary, name=QDK1, step=0.01;

  m1: macro=
   {
      ptc_create_universe;
      ptc_create_layout, model=1, method=6, nst=10, exact=true, closed_layout=true;
      ptc_setswitch, debuglevel=0, exact_mis=true, time=true, totalpath=true;

      ptc_twiss, closed_orbit, table=twiss, icase=4, no=4;

      ptc_normal, closed_orbit,normal,icase=4, no=4;

      value, table(twiss,PSR$END,beta11);
      value, table(twiss,PSR$END,beta22);
      value, table(H3,PSR$END,h0000);
      value, dQxdX, dQydX, dQxdX2, dQydX2;
      value, SD0;

      ptc_end;

   };


   constraint, expr=table(H3,PSR$END,h0000) = 0.0;

   constraint, expr= dQxdX   <1e-3;
   constraint, expr= dQxdX2  <1e-3;



   lmdif,  tolerance=1e-30 ,calls=200;

endmatch;

stop;
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!
!  SSSSS  TTTTT   OOO   PPP
!  S        T    O   O  P  P
!  SSSSS    T    O   O  PPP
!      S    T    O   O  P
!  SSSSS    T     OOO   P
!

